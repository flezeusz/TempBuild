plugins {
    id 'java'
    id "com.gradleup.shadow" version "9.3.1"
}

group = 'pl.flezy'
version = '1.0.2'

repositories {
    mavenCentral()

    maven {
        name = "papermc"
        url = "https://repo.papermc.io/repository/maven-public/"
    }

    // Okaeri Configs
    maven { url = "https://storehouse.okaeri.eu/repository/maven-public/" }

    // WorldGuard
    maven { url "https://maven.enginehub.org/repo/" }
}

dependencies {
    compileOnly 'io.papermc.paper:paper-api:1.19.2-R0.1-SNAPSHOT'

    compileOnly 'com.sk89q.worldguard:worldguard-bukkit:7.0.8'

    implementation 'eu.okaeri:okaeri-configs-yaml-bukkit:5.0.9'
    implementation 'eu.okaeri:okaeri-configs-serdes-bukkit:5.0.9'
    implementation 'eu.okaeri:okaeri-configs-serdes-commons:5.0.9'
    implementation 'eu.okaeri:okaeri-configs-validator-okaeri:5.0.9'
    implementation 'eu.okaeri:okaeri-placeholders-bukkit:5.1.1'
}

shadowJar {
    relocate("eu.okaeri", "pl.flezy.tempbuild.libs.eu.okaeri")
    archiveClassifier.set('')
}

assemble.dependsOn(shadowJar)

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
